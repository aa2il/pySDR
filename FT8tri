#! /bin/tcsh
###############################################################################
#
# FT8tri - Rev 1.0
# Copyright (C) 2021 by Joseph B. Attili, aa2il AT arrl DOT net
#
# Script to start 3-band monitoring of FT8
#
# WSJTX Settings:
#       SDR1  - localhost:4575,  monitor for aloop_0, udp port 2238
#       SDR2  - localhost:4576,  monitor for aloop_1, udp port 2239
#       SDR3  - localhost:4577,  monitor for aloop_2, udp port 2240
#       SDR14 - localhost:4577,  monitor for aloop_3, udp port TBD
#       SDR24 - localhost:4578,  monitor for aloop_4, udp port TBD
#       SDR34 - localhost:4578,  monitor for aloop_5, udp port TBD
#
###############################################################################

# User Params

# Set rig to HAMLIB if want to compare decodes from SDR and rig
#set RIG="-rig HAMLIB"
set RIG="-rig NONE"

set FRQS = "1840 3573 7074 10136 14074 18100 21074 24915 28075 50313"

#set FRQS = "10136 14074 18100"
set FRQS = "7074 10136 14074"
#set FRQS = "14074 18100 21074"
set FRQS = "18100 21074 24915"

#set FRQS = "600 640 760 1170"
#set FRQS = "600 640 760"
#set FRQS = "600 760"
#set FRQS = "600"

set HOP_TIME=150
#set HOP_TIME=30

set RTL="-rtlhf"
set RTL=""

set LOOPBACK="-loopback"
#set LOOPBACK=""

set FS=8

# Turn this on to monitor FT4 sub-bands also
set FT4=""
#set FT4="-ft4"
#set FT4="-ft44"

set HOPPER="$FT4 -hop -hop_time $HOP_TIME"
#set HOPPER="$FT4"

set SDR_OPTS="-vid_bw 45"

set VERSION="2.4.0"

###############################################################################

cd ~/Python/pySDR
set PY_SDR = "pySDR.py"
#set PY_SDR = "mp.py"

start_loopback

pkill pySDR
pkill mp.py
pkill bandmap
pkill rigctld

echo "\nStarting pySDR ..."
set cmd="$PY_SDR $RTL -fc $FRQS -fs $FS -IF 0 -foffset 0 -mode USB -fsout 48 -af_bw 5 $HOPPER $LOOPBACK $RIG $SDR_OPTS"
echo $cmd
echo $cmd >& /tmp/SDR 
$cmd >>& /tmp/SDR &
#exit

sleep 1
echo Waiting for SDR to start up ...
set id=`find_windows "pySDR" 20`
while( $#id<1 )
    set id=`find_windows "pySDR" 10`
    sleep 1
end

###############################################################################

# Sometimes we need to use pavucontrol to direct audio to right place
echo "\nStarting WSJT-X ..."
set EXE_DIR="~/wsjtx-${VERSION}/build/wsjtx-prefix/src/wsjtx-build"
$EXE_DIR/wsjtx -r SDR1 &
sleep 1
$EXE_DIR/wsjtx -r SDR2 &
sleep 1
$EXE_DIR/wsjtx -r SDR3 &
sleep 1
set NWIN=6

if( "$FT4" != "" )then
    $EXE_DIR/wsjtx -r SDR14 &
    sleep 1
    $EXE_DIR/wsjtx -r SDR24 &
    sleep 1
    $EXE_DIR/wsjtx -r SDR34 &
    sleep 1
    set NWIN=12
    #exit
endif

#if( "$FT4" == "-ft44" )then
#    wsjtx -r SDR344 &
#    sleep 1
#    exit
#endif

echo Waiting for WSJT to start...
set id=`find_windows "WSJT" 10`
while( $#id < $NWIN )
    set id=`find_windows "WSJT" 10`
    sleep 1
end

###############################################################################

echo "\nStarting BANDMAPS ..."
rm /tmp/BANDMAP*
cd ~/Python/bandmap
set BANDMAP=~/Python/bandmap/bandmap.py
set cmd="$BANDMAP -rig NONE -wsjt SDR1 2238 -dx"
echo $cmd >& /tmp/BANDMAP1
$cmd >>& /tmp/BANDMAP1 &
sleep 1

set cmd="$BANDMAP -rig NONE -wsjt SDR2 2239 -dx"
echo $cmd >& /tmp/BANDMAP2 
$cmd >>& /tmp/BANDMAP2 &
sleep 1

set cmd="$BANDMAP -rig NONE -wsjt SDR3 2240 -dx"
echo $cmd >& /tmp/BANDMAP3 
$cmd >>& /tmp/BANDMAP3 &
sleep 1
set NWIN=3

if( "$FT4" != "" )then
    $BANDMAP -rig NONE -wsjt SDR14 -dx >& /tmp/BANDMAP14 &
    sleep 1
    $BANDMAP -rig NONE -wsjt SDR24 -dx >& /tmp/BANDMAP24 &
    sleep 1
    $BANDMAP -rig NONE -wsjt SDR34 -dx >& /tmp/BANDMAP34 &
    sleep 1
    set NWIN=6
endif

echo Waiting for bandmaps to start...
set id=`find_windows "Band Map" 10`
while( $#id < $NWIN )
    set id=`find_windows "Band Map" 10`
    sleep 1
end

###############################################################################

# Organize windows
organize_windows
